import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply plugin: 'com.archinamon.aspectj'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "ds.meterscanner"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode 2
        versionName "1.4 build" + new SimpleDateFormat("MMdd").format(new Date())
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    flavorDimensions "abi"
    productFlavors {
        x86 {
            dimension "abi"
            ndk.abiFilter "x86"
        }
        arm {
            dimension "abi"
            ndk.abiFilter "armeabi-v7a"
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    def supportVersion = '26.1.0'
    def firebaseVersion = '11.4.2'
    def retrofitVersion = '2.3.0'
    def acVersion = '1.0.0'
    def kodeinVersion = '4.1.0'

    // supportlib stuff
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:percent:$supportVersion"
    implementation "com.android.support:gridlayout-v7:$supportVersion"
    implementation "com.android.support:preference-v7:$supportVersion"
    implementation "com.android.support:preference-v14:$supportVersion"
    implementation "com.takisoft.fix:preference-v7:26.1.0.3"                                        // https://github.com/Gericop/Android-Support-Preference-V7-Fix
    implementation "com.android.support:multidex:1.0.2"

    implementation "com.google.firebase:firebase-auth:$firebaseVersion"
    implementation "com.google.firebase:firebase-database:$firebaseVersion"
    implementation "com.google.firebase:firebase-messaging:$firebaseVersion"
    implementation "com.google.firebase:firebase-storage:$firebaseVersion"
    implementation "com.google.firebase:firebase-config:$firebaseVersion"

    // networking
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"                               // https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:3.8.1"
    //noinspection GradleDependency
    implementation "com.github.bumptech.glide:glide:3.8.0"                                          // https://github.com/bumptech/glide
    implementation "com.evernote:android-job:1.2.0"                                                 // https://github.com/evernote/android-job

    // injectors
    implementation "com.github.salomonbrys.kodein:kodein:$kodeinVersion"                            // https://salomonbrys.github.io/Kodein/
    implementation "com.github.salomonbrys.kodein:kodein-conf:$kodeinVersion"
    implementation "com.github.salomonbrys.kodein:kodein-android:$kodeinVersion"
    //implementation "com.evernote:android-state:1.1.1"                                             // https://github.com/evernote/android-state
    //kapt "com.evernote:android-state-processor:1.1.1"

    // ui
    implementation ("com.wdullaer:materialdatetimepicker:3.3.1") {                                  // https://github.com/wdullaer/MaterialDateTimePicker
        exclude group: "com.android.support", module: "support-v13"
    }
    implementation "com.github.lecho:hellocharts-library:1.5.8@aar"                                 // https://github.com/lecho/hellocharts-android

    // etc
    implementation "io.anyline:anylinesdk:3.11.0@aar"                                               // https://www.anyline.io/download/
    //implementation "org.greenrobot:eventbus:3.0.0"                                                // https://github.com/greenrobot/EventBus
    //kapt "org.greenrobot:eventbus-annotation-processor:3.0.1"                                     // see kapt params
    implementation "com.jakewharton.timber:timber:4.5.1"                                            // https://github.com/JakeWharton/timber
    implementation "com.github.deviant-studio:bindingtools:0.14"                                    // https://github.com/deviant-studio/bindingtools

    // development
    implementation "android.arch.lifecycle:extensions:$acVersion"
    kapt "android.arch.lifecycle:compiler:$acVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"              // https://github.com/Kotlin/kotlinx.coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "ru.gildor.coroutines:kotlin-coroutines-retrofit:0.8.2"                          // https://github.com/gildor/kotlin-coroutines-retrofit
    debugImplementation "com.facebook.stetho:stetho:1.5.0"                                          // https://github.com/facebook/stetho
    debugImplementation "com.facebook.stetho:stetho-okhttp3:1.5.0"
    provided "com.facebook.stetho:stetho:1.5.0"
    provided "com.facebook.stetho:stetho-okhttp3:1.5.0"
    debugImplementation "io.palaima.debugdrawer:debugdrawer:0.7.0"
    releaseImplementation "io.palaima.debugdrawer:debugdrawer-no-op:0.7.0"
    implementation "io.palaima.debugdrawer:debugdrawer-actions:0.7.0"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.5.4"                          // https://github.com/square/leakcanary
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.5.4"

    // tests
    implementation fileTree(dir: "libs", include: ["*.jar"])
    testImplementation "junit:junit:4.12"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"

}

apply plugin: "com.google.gms.google-services"
